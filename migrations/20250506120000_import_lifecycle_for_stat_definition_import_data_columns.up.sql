BEGIN;

-- Lifecycle callback procedures for statistical_variables data columns
CREATE OR REPLACE PROCEDURE import.generate_stat_var_data_columns()
LANGUAGE plpgsql AS $$
DECLARE
    v_step_id INT;
    v_stat_def RECORD;
    v_def RECORD;
    v_pk_col_name TEXT;
    v_stat_def_code TEXT;
    v_current_priority INT;
    v_active_codes TEXT[];
BEGIN
    SELECT id INTO v_step_id FROM public.import_step WHERE code = 'statistical_variables';
    IF v_step_id IS NULL THEN
        RAISE EXCEPTION 'statistical_variables step not found, cannot generate data columns.';
        RETURN;
    END IF;

    SELECT array_agg(code ORDER BY priority) INTO v_active_codes FROM public.stat_definition_active;
    RAISE DEBUG '[import.generate_stat_var_data_columns] For step_id % (statistical_variables), ensuring data columns for active codes: %', v_step_id, v_active_codes;

    SELECT COALESCE(MAX(idc.priority), 0) INTO v_current_priority
    FROM public.import_data_column idc WHERE idc.step_id = v_step_id;

    -- Add source_input columns for each active stat_definition
    FOR v_stat_def IN SELECT code FROM public.stat_definition_active ORDER BY priority
    LOOP
        v_current_priority := v_current_priority + 1;
        INSERT INTO public.import_data_column (step_id, column_name, column_type, purpose, is_nullable, is_uniquely_identifying, priority)
        VALUES (v_step_id, v_stat_def.code, 'TEXT', 'source_input', true, false, v_current_priority)
        ON CONFLICT (step_id, column_name) DO UPDATE SET
            priority = EXCLUDED.priority,
            is_uniquely_identifying = EXCLUDED.is_uniquely_identifying;
    END LOOP;

    -- Add pk_id columns for each active stat_definition
    FOR v_stat_def IN SELECT code FROM public.stat_definition_active ORDER BY priority
    LOOP
        v_current_priority := v_current_priority + 1;
        v_pk_col_name := format('stat_for_unit_%s_id', v_stat_def.code);
        INSERT INTO public.import_data_column (step_id, column_name, column_type, purpose, is_nullable, is_uniquely_identifying, priority)
        VALUES (v_step_id, v_pk_col_name, 'INTEGER', 'pk_id', true, false, v_current_priority)
        ON CONFLICT (step_id, column_name) DO UPDATE SET
            priority = EXCLUDED.priority,
            is_uniquely_identifying = EXCLUDED.is_uniquely_identifying;
    END LOOP;
END;
$$;

CREATE OR REPLACE PROCEDURE import.cleanup_stat_var_data_columns()
LANGUAGE plpgsql AS $$
DECLARE
    v_step_id INT;
    v_pk_col_name TEXT;
BEGIN
    SELECT id INTO v_step_id FROM public.import_step WHERE code = 'statistical_variables';
    IF v_step_id IS NULL THEN
        RAISE WARNING 'statistical_variables step not found, cannot clean up data columns.';
        RETURN;
    END IF;

    RAISE DEBUG '[import.cleanup_stat_var_data_columns] For step_id % (statistical_variables), deleting all source_input and pk_id columns.', v_step_id;

    -- Delete columns dynamically added by the generate procedure
    DELETE FROM public.import_data_column
    WHERE step_id = v_step_id
      AND (
          -- Delete ALL source_input columns for this step, as they are all dynamically generated
          -- by this callback based on stat_definition entries.
          (purpose = 'source_input')
          OR
          -- Delete the pk_id columns matching the pattern, also generated by this callback.
          (purpose = 'pk_id' AND column_name LIKE 'stat_for_unit_%_id')
      );
END;
$$;

-- Register the lifecycle callback
CALL lifecycle_callbacks.add(
    'import_stat_var_data_columns',
    ARRAY['public.stat_definition']::regclass[],
    'import.generate_stat_var_data_columns',
    'import.cleanup_stat_var_data_columns'
);

-- Call generate once initially to populate columns based on existing definitions
CALL import.generate_stat_var_data_columns();

END;
